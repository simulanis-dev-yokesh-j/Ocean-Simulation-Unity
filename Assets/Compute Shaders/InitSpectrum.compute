// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CalculateInitSpectrum
#pragma kernel ConjugateInitSpectrum

#define PI 3.1415926
#define G 9.81

// Inputs
uint Size;
uint LengthScale;
Texture2D Noise;

float LowCutoff;
float HighCutoff;
float WindSpeed;
float WindAngle;
float Swell;
float Fetch;
float Depth;

// Outputs
RWTexture2D<float2> InitSpectrum;
RWTexture2D<float4> ConjugatedSpectrum;
RWTexture2D<float4> WaveData;

float DispersionPeak()
{
    return 22 * pow(G * G / (WindSpeed * Fetch), 0.33);
}

float WaveDispersion(float kLength)
{
    return sqrt(G * kLength);
}

float DispersionDerivative(float kLength)
{
    return G / (2 * sqrt(G * kLength));
}

float fmod(float a, float b)
{
    return a - b * floor(a / b);
}

float GammaApprox(float x)
{
    float firstPart = sqrt(2 * PI / x) * pow(x / exp(1), x);
    float secondPart = 1 + 1 / (12 * x) + 1 / (288 * x * x) - 139 / (51840 * x * x * x) - 571 / (2488320 * x * x * x * x);
    return firstPart * secondPart;
}

float NormalizationFactor(float s)
{
    float firstPart = pow(2, 2*s - 1) / PI;
    float secondPart = pow(GammaApprox(s+1), 2) / GammaApprox(2*s + 1);
    return firstPart * secondPart;
}

float WaveAngle(float2 k)
{
    const float windAngle = WindAngle / 180 * PI;
    float angle = atan2(k.y, k.x) - windAngle;

    // Normalize the angle to the range [-PI, PI]
    angle = fmod(angle + PI, 2 * PI);
    if (angle < 0)
        angle += 2 * PI;
    return angle - PI;
}

float TMACorrection(float dispersion)
{
    float omegaH = dispersion * sqrt(Depth / G);

    if(omegaH <= 1)
        return 0.5 * omegaH * omegaH;
    if(omegaH < 2)
        return 1 - 0.5 * (2 - omegaH) * (2 - omegaH);
    
    return 1; 
}

float JONSWAP(float dispersion)
{
    float alpha = 0.076 * pow(WindSpeed * WindSpeed / (Fetch * G), 0.22);
    float omega_p = DispersionPeak();
    float sigma = dispersion <= omega_p ? 0.07 : 0.09;
    float r = exp(-(dispersion - omega_p) * (dispersion - omega_p) / (2 * sigma * sigma * omega_p * omega_p));
    
    float firstPart = alpha * G * G / (dispersion * dispersion * dispersion * dispersion * dispersion);
    float secondPart = exp(-1.25 * pow(omega_p / dispersion, 4));
    float thirdPart = pow(3.3, r);

    return TMACorrection(dispersion) * firstPart * secondPart * thirdPart;
}

float BaseSpread(float dispersion, float angle)
{
    float omega_p = DispersionPeak();
    float omegaOverOmegaPeek = dispersion / omega_p;
    float beta;

    if(omegaOverOmegaPeek < 0.95)
    {
        beta = 2.61 * pow(omegaOverOmegaPeek, 1.3);
    }
    else if(0.95 <= omegaOverOmegaPeek && omegaOverOmegaPeek <= 1.6)
    {
        beta = 2.28 * pow(omegaOverOmegaPeek, -1.3);
    }
    else
    {
        float epsilon = -0.4 + 0.8393 * exp(-0.567 * log(omegaOverOmegaPeek * omegaOverOmegaPeek));
        beta = pow(10, epsilon);
    }
    
    float sech = 1 / cosh(beta * angle);
    
    float firstPart = beta / (2 * tanh(beta * PI));
    float secondPart = sech * sech;

    return firstPart * secondPart;
}

float SweelDirection(float dispertion, float angle)
{
    float s = 16 * tanh(DispersionPeak() / dispertion) * Swell * Swell;
    return NormalizationFactor(s) * pow(abs(cos(angle/2)), 2 * s);
}

float DirectionalSpread(float dispersion, float angle)
{
    float base = BaseSpread(dispersion, angle);
    float sweel = SweelDirection(dispersion, angle);
    return base * sweel;
}

float IntegratedDirectionalSpread(float dispersion)
{
    float step = 0.01;
    float sum = 0;
    for(float angle = -PI; angle < PI; angle += step)
    {
        sum += DirectionalSpread(dispersion, angle) * step;
    }

    return 1/sum;
}

float FinalDirectionalSpread(float dispersion, float angle)
{
    float integration = IntegratedDirectionalSpread(dispersion);
    float base = BaseSpread(dispersion, angle);
    float swell = SweelDirection(dispersion, angle);
    return integration * base * swell;
}

float Spectrum(float kLength, float dispersion, float angle)
{
    return JONSWAP(dispersion) * FinalDirectionalSpread(dispersion, angle) * DispersionDerivative(kLength) / kLength;
}

float2 FourierWaveAmplitude(float2 pos, float2 k, float dispersion, float angle)
{
    float deltaK = 2 * PI / LengthScale;
    float kLength = length(k);
    float2 rand = float2(Noise[pos.xy].x, Noise[pos.xy].y);

    return rand * sqrt(2 * Spectrum(kLength, dispersion, angle) * deltaK * deltaK);
}

float2 WaveVector(float2 pos)
{
    float n = Size / 2;
    float k_x = 2 * PI * (pos.x - n) / LengthScale;
    float k_z = 2 * PI * (pos.y - n) / LengthScale;
    float2 k = float2(k_x, k_z);

    if (length(k) == 0)
    {
        k.x = 0.0001;
        k.y = 0.0001;
    }
    
    return k;
}

[numthreads(8,8,1)]
void CalculateInitSpectrum (uint3 id : SV_DispatchThreadID)
{
    float2 k = WaveVector(id.xy);
    float kLength = length(k);
    float dispersion = WaveDispersion(kLength);
    float angle = WaveAngle(k);
    
    if(kLength > LowCutoff && kLength < HighCutoff)
    {
        float2 h_0 = FourierWaveAmplitude(id.xy, k, dispersion, angle);
        InitSpectrum[id.xy] = float2(h_0.xy);
        WaveData[id.xy] = float4(k.x, k.y, dispersion, 1);
    }
    else
    {
        InitSpectrum[id.xy] = float2(0, 0);
        WaveData[id.xy] = float4(k.x, k.y, 1, 1);
    }
}

[numthreads(8,8,1)]
void ConjugateInitSpectrum (uint3 id : SV_DispatchThreadID)
{
    float2 h_0 = InitSpectrum[id.xy];
    float2 h_minus_k = InitSpectrum[uint2((Size - id.x) % Size, (Size - id.y) % Size)];
    ConjugatedSpectrum[id.xy] = float4(h_0.xy, h_minus_k.x, -h_minus_k.y);
}
